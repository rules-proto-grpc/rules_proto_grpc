load("@rules_java//java:defs.bzl", "java_binary")
load("@rules_proto_grpc//:defs.bzl", "proto_plugin")
load("@rules_scala_config//:config.bzl", "SCALA_VERSIONS")

proto_plugin(
    name = "proto_plugin",
    out = "{name}.jar",
    tool = ":scala_pb_plugin_bin",
    use_built_in_shell_environment = True,
    visibility = ["//visibility:public"],
)

proto_plugin(
    name = "grpc_plugin",
    out = "{name}.jar",
    options = [
        "grpc",
    ],
    tool = ":scala_pb_plugin_bin",
    use_built_in_shell_environment = True,
    visibility = ["//visibility:public"],
)

java_binary(
    name = "scala_pb_plugin_bin",
    main_class = "scalapb.ScalaPbCodeGenerator",
    runtime_deps = select({  # Can't use select_for_scala_version as it doesn't use Label()
        "@rules_scala_config//:scala_version_{}".format(scala_version.replace(".", "_")): [
            "@rules_proto_grpc_scala_maven_{0}//:com_thesamet_scalapb_compilerplugin_{0}".format(
                "3" if scala_version.startswith("3") else scala_version.replace(".", "_").rpartition("_")[0],
            ),
        ]
        for scala_version in SCALA_VERSIONS
    }),
)
